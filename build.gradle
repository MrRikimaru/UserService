plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.sonarqube' version '4.4.1.3373'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'UserService'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	testcontainersVersion = "1.19.7"
}

dependencies {
	// Основные зависимости
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.liquibase:liquibase-core'
	runtimeOnly 'org.postgresql:postgresql'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.apache.commons:commons-pool2'
	implementation 'org.springframework.boot:spring-boot-starter-logging'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Тестирование
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'

	// TestContainers - используем BOM для согласованных версий
	testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'com.redis:testcontainers-redis:2.2.4'
	testImplementation 'org.testcontainers:testcontainers'

	// Mockito
	testImplementation 'org.mockito:mockito-core'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed", "standard_out", "standard_error"
		exceptionFormat "full"
		showExceptions true
		showCauses true
		showStackTraces true
	}

	systemProperties = [
			'spring.profiles.active': 'test',
			'DB_HOST': 'localhost',
			'DB_PORT': '5432',
			'DB_NAME': 'testdb',
			'DB_USERNAME': 'testuser',
			'DB_PASSWORD': 'testpassword',
			'REDIS_HOST': 'localhost',
			'REDIS_PORT': '6379'
	]

	maxParallelForks = 2
	forkEvery = 100

	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.11"
	reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
		xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/dto/**',
					'**/entity/**',
					'**/config/**',
					'**/exception/**',
					'**/UserServiceApplication.class'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport
	violationRules {
		rule {
			limit {
				minimum = 0.6
			}
		}
	}
}

// SonarQube configuration
sonarqube {
	properties {
		property "sonar.projectKey", "MrRikimaru_UserService"
		property "sonar.organization", "mrrikimaru"
		property "sonar.host.url", "https://sonarcloud.io"
		property 'sonar.login', System.getenv('SONAR_TOKEN') ?: ''
		property "sonar.sourceEncoding", "UTF-8"
		property 'sonar.java.source', '21'
	}
}

tasks.named('sonar') {
	dependsOn jacocoTestReport
}

spotless {
	java {
		target 'src/**/*.java'
		googleJavaFormat()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

compileJava {
	options.compilerArgs = [
			'-Amapstruct.defaultComponentModel=spring',
			'-parameters'
	]
}

bootJar {
	archiveFileName = 'app.jar'
	enabled = true
}

jar {
	enabled = false
}